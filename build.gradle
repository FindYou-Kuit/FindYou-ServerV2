plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
}

jacoco {
	toolVersion = "0.8.12"
}

group = 'com.kuit'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring data JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// Validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// Web
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'

	// Devtools
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// h2
	runtimeOnly 'com.h2database:h2'

	// MySQL
	runtimeOnly 'com.mysql:mysql-connector-j'

	// Test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Mock
	testImplementation 'org.mockito:mockito-core'
	testImplementation 'org.mockito:mockito-junit-jupiter'

	// RestAssured
	testImplementation 'io.rest-assured:rest-assured'

	// Swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

	// JWT
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-impl:0.12.3'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.12.3'

	// Security
	implementation("org.springframework.boot:spring-boot-starter-security")
	testImplementation("org.springframework.security:spring-security-test")

	// AWS
	implementation platform('software.amazon.awssdk:bom:2.25.8')
	implementation 'software.amazon.awssdk:s3'

	// Resilience4J
	implementation 'io.github.resilience4j:resilience4j-spring-boot3:2.2.0'

	// Redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// Testcontainer
	testImplementation 'org.testcontainers:testcontainers:1.19.3'
	testImplementation 'org.testcontainers:junit-jupiter:1.19.3'

	// Wiremock
	testImplementation("org.wiremock:wiremock-standalone:3.2.0")

	// Flyway
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-mysql'

	// Jackson xml parsing
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport // 테스트 후 자동으로 커버리지 리포트 생성
}

// Jacoco 테스트 리포트 설정
jacocoTestReport {
	dependsOn test // 테스트가 선행되어야 함

	reports {
		xml.required = true // CI 에서 파싱하기 위해 필요
		csv.required = false
		html.required = true // 개발자가 브라우저에서 보기 위해 필요
	}

	// 커버리지에서 제외할 클래스들
	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'**/config/**',          // 설정 클래스 제외
					'**/dto/**',             // DTO 클래스 제외
					'**/model/**',           // JPA 엔티티 제외
					'**/*Application.class', // 메인 애플리케이션 클래스 제외
					'**/exception/**',       // 예외 클래스 제외
					'**/common/**',          // 공통 클래스 제외
					'**/constant/**',        // ENUM 클래스 제외
					'**/logging/**',         // 로그 관련 클래스 제외
					'**/notification/**',    // 알림 관련 클래스 제외
					'**/test/**',            // 테스트용 클래스 제외
					'**/infrastructure/**'   // S3 관련 클래스 제외

			])
		}))
	}

	finalizedBy jacocoTestCoverageVerification  // 리포트 생성 후 커버리지 검증
}

// 커버리지 검증 규칙
jacocoTestCoverageVerification {
	dependsOn jacocoTestReport

	violationRules {
		rule {
			// 전체 프로젝트 커버리지 규칙
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.00  // 전체 라인 커버리지
			}
		}

		rule {
			// 브랜치 커버리지 규칙
			limit {
				counter = 'BRANCH'
				value = 'COVEREDRATIO'
				minimum = 0.00  // 브랜치 커버리지
			}
		}

		rule {
			// 개별 클래스 커버리지 규칙
			element = 'CLASS'
			excludes = [
					'*.config.*',        	 // 설정 클래스 제외
					'*.dto.*',           	 // DTO 클래스 제외
					'*.model.*',         	 // 엔티티 클래스 제외
					'*Application',      	 // 메인 애플리케이션 클래스 제외
					'*.exception.*',     	 // 예외 클래스 제외
					'*.common.*',         	 // 공통 클래스 제외
					'*.constant.*',      	 // ENUM 클래스 제외
					'*.logging.*',       	 // 로그 관련 클래스 제외
					'*.notification.*',  	 // 알림 관련 클래스 제외
					'*.test.*',         	 // 테스트용 클래스 제외
					'*.infrastructure.*'   // S3 관련 클래스 제외
			]

			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.00  // 개별 클래스
			}
		}
	}
}

